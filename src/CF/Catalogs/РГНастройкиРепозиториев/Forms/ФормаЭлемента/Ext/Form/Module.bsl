///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Библиотека работы с Git.                                                                                         //
//	Для работы требуется библиотека работы с безопасным хранилищем (общий модуль SDS_ОбщегоНазначения),              // 
//	либо использование БСП при условии замены в коде вызовов "SDS_ОбщегоНазначения" на "ОбщегоНазначения".           //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтображениеНастроекАвторизации();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	СохранитьНастройкиАвторизации();
КонецПроцедуры         

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособАвторизацииПриИзменении(Элемент)
	УстановитьОтображениеНастроекАвторизации();  
КонецПроцедуры

&НаКлиенте
Процедура СпособАвторизацииНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СтандартнаяОбработка = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",
		      ЭтотОбъект);	
		 
	    ПоказатьВопрос(Оповещение,
	        "Продолжить?",
	        РежимДиалогаВопрос.ДаНет,
	        0, 
	        КодВозвратаДиалога.Да, 
	        "Для продолжения необходимо сохранить настройку");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутьКРепозиториюПриИзменении(Элемент)
	Объект.ПутьЛокальногоХранения = СокрЛП(Объект.ПутьЛокальногоХранения);
	Если Прав(Объект.ПутьЛокальногоХранения, 1) = "\" Тогда
		Объект.ПутьЛокальногоХранения = Лев(Объект.ПутьЛокальногоХранения, СтрДлина(Объект.ПутьЛокальногоХранения) - 1);	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтображениеНастроекАвторизации()
	Элементы.ГруппаАвторизацияOAuth.Видимость = 
										Объект.СпособАвторизации = ПредопределенноеЗначение("Перечисление.РГСпособыАвторизации.OAuth");
	
	Элементы.ГруппаАвторизацияЛогинПароль.Видимость = 
										Объект.СпособАвторизации = ПредопределенноеЗначение("Перечисление.РГСпособыАвторизации.ПоПаролю");																	
	ПроверитьНаличиеДанныхАвторизации();
КонецПроцедуры  

&НаКлиенте
Процедура ПроверитьНаличиеДанныхАвторизации()
	ДанныеАвторизации = ПолучитьДанныеАвторизации();
	Элементы.ГруппаУправлениеАвторизацией.Видимость = ЗначениеЗаполнено(ДанныеАвторизации);
	Элементы.ГруппаВариантыАвторизации.Видимость = Не Элементы.ГруппаУправлениеАвторизацией.Видимость;
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеАвторизации()	
	Если Объект.СпособАвторизации = ПредопределенноеЗначение("Перечисление.РГСпособыАвторизации.OAuth") Тогда
		ДанныеАвторизации = РГПолныеПрава.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Токен");
	ИначеЕсли Объект.СпособАвторизации = ПредопределенноеЗначение("Перечисление.РГСпособыАвторизации.ПоПаролю") Тогда
		ДанныеАвторизации = РГПолныеПрава.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Логин");
	Иначе
		ДанныеАвторизации = Неопределено;	
	КонецЕсли;
	Возврат ДанныеАвторизации;
КонецФункции

&НаСервере
Процедура СохранитьНастройкиАвторизации()
	Если Объект.СпособАвторизации = Перечисления.РГСпособыАвторизации.ПоПаролю И ЗначениеЗаполнено(Логин) Тогда
		РГПолныеПрава.ЗаписатьДанныеВБезопасноеХранилище(Объект.Ссылка, СокрЛП(Логин), "Логин");
		РГПолныеПрава.ЗаписатьДанныеВБезопасноеХранилище(Объект.Ссылка, СокрЛП(Пароль));	
	ИначеЕсли Объект.СпособАвторизации = Перечисления.РГСпособыАвторизации.OAuth И ЗначениеЗаполнено(Токен) Тогда
		РГПолныеПрава.ЗаписатьДанныеВБезопасноеХранилище(Объект.Ссылка, СокрЛП(Токен), "Токен");
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Проверьте заполнение данных авторизации";
		Сообщение.Сообщить();	
	КонецЕсли;
КонецПроцедуры
 
&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт 
    Если Результат = КодВозвратаДиалога.Да Тогда
        Записать();
    КонецЕсли;	 
КонецПроцедуры

&НаСервере
Процедура ОчиститьДанныеАвторизацииНаСервере()
	Если Объект.СпособАвторизации = Перечисления.РГСпособыАвторизации.ПоПаролю Тогда
		РГПолныеПрава.УдалитьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Логин, Пароль");	
	ИначеЕсли Объект.СпособАвторизации = Перечисления.РГСпособыАвторизации.OAuth Тогда
		РГПолныеПрава.УдалитьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Токен");
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выбран неизвестный тип авторизации";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДанныеАвторизации(Команда)
	ОчиститьДанныеАвторизацииНаСервере();
	ПроверитьНаличиеДанныхАвторизации();
КонецПроцедуры

&НаСервере
Процедура ПутьЛокальногоХраненияИзменениеТекстаРедактированияНаСервере()
	Элементы.Инициализирован.Доступность =
		Объект.Инициализирован И Объект.ПутьЛокальногоХранения <> Объект.Ссылка.ПутьЛокальногоХранения;
КонецПроцедуры

&НаКлиенте
Процедура ПутьЛокальногоХраненияИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 
	Объект.ПутьЛокальногоХранения = Текст;
	КонецСтроки = СтрДлина(Текст) + 1;
	Элементы.ПутьЛокальногоХранения.УстановитьГраницыВыделения(КонецСтроки, КонецСтроки);
	ПутьЛокальногоХраненияИзменениеТекстаРедактированияНаСервере();
КонецПроцедуры

#КонецОбласти
